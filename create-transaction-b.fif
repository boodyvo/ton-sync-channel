#!/usr/bin/env fift -s
"TonUtil.fif" include
"Asm.fif" include

{ ."usage: " @' $0 type ." <amount-to-send> <private-key-file>" cr
  ."Creates offchain transaction."
  cr 1 halt
} : usage
$# 2 <> ' usage if

$1 parse-int dup =: amount-b  // amount that user wants to send
64 fits ' usage ifnot
$2 constant private-key-file

private-key-file file>B dup Blen 32 <> abort"Private key must be exactly 32 bytes long" constant private-key

// read last state

"last-confirmed-state-b.boc" file>B B>boc
<s 
64 u@+ swap =: last-height
64 u@+ swap =: last-amount-a
64 u@+ swap =: last-amount-b
64 B@ =: last-signature-b

// validate that user have enough money to send

last-amount-b amount-b >= { ."amount should be not greater than " last-amount-b . ."nanograms." cr 1 halt } ifnot

last-height 1+ =: new-height
last-amount-a amount-b + =: new-amount-a
last-amount-b amount-b - =: new-amount-b

<b new-height 64 u, new-amount-a 64 u, new-amount-b 64 u, b>

dup hashu private-key ed25519_sign_uint swap

<b swap <s s, swap B, b>

2 boc+>B "last-state-candidate-a.boc" tuck B>file

."ok" cr